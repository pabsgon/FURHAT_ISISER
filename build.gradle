
plugins {
    id "org.jetbrains.kotlin.jvm" version "1.8.21"
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'java'
apply plugin: 'kotlin'

//Defines what version of Java to use.
sourceCompatibility = 1.8

//Defines how Kotlin should compile.
compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        //Defines what jvm bytecode to use, 1.8 rather than 1.6
        jvmTarget = "1.8"
        apiVersion = "1.8"
        languageVersion = "1.8"
    }
}

//Defines how Kotlin should compile when testingTry to keep it the same as compileKotlin.
compileTestKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        //Defines what jvm bytecode to use, 1.8 rather than 1.6
        jvmTarget = "1.8"
        apiVersion = "1.8"
        languageVersion = "1.8"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://s3-eu-west-1.amazonaws.com/furhat-maven/releases"}
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

ext {
    ktor_version = '2.3.1'
}

dependencies {
    implementation 'com.furhatrobotics.furhatos:furhat-commons:2.7.0'
    //Libraries to connect Furhat to GUI:
    implementation "io.ktor:ktor-serialization-jackson:$ktor_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-server-freemarker:$ktor_version"
    implementation "io.ktor:ktor-server-status-pages:$ktor_version"
    implementation "io.ktor:ktor-server-websockets:$ktor_version"
    implementation "io.ktor:ktor-server-cors:$ktor_version"
    implementation "io.ktor:ktor-serialization-kotlinx-json:$ktor_version"
    implementation("io.ktor:ktor-server-content-negotiation:$ktor_version")
    //implementation("io.ktor:ktor-client-content-negotiation:$ktor_version")
    //Logging via logback library:
    implementation 'ch.qos.logback:logback-classic:1.2.11'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    //This is for accessing Google Sheets:
    implementation 'com.google.api-client:google-api-client:1.30.10'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.31.0'
    implementation 'com.google.apis:google-api-services-sheets:v4-rev581-1.25.0'

}

//These new blocks are needed to package your project into a working skill file.
jar {
    def lowerCasedName = baseName.toLowerCase()
    def normalizedName = lowerCasedName.substring(0,1).toUpperCase() + lowerCasedName.substring(1)
    manifest.attributes(
            'Class-Path': configurations.compileClasspath.collect { it.getName() }.join(' '),
            'Main-Class': "furhatos.app.${lowerCasedName}.${normalizedName}Skill"
    )
}

shadowJar {
    manifest {
        exclude '**/Log4j2Plugins.dat'
        exclude '**/node_modules'
    }
    from "skill.properties"
    from "assets"
    extension 'skill'
}
